// Copyright 2018 Alex Badics <admin@stickman.hu>
//
// This file is part of Hun-Law.
//
// Hun-Law is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Hun-Law is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Hun-Law.  If not, see <https://www.gnu.org/licenses/>.

start: regular_sentence+

regular_sentence: any_expression+ END_OF_SENTENCE

?any_expression: WORD
    | NUMBER
    | NUMBER_WITH_DOT
    | PARENTHESES
    | compound_reference
    | quoted_text

compound_reference: (act_reference reference?) | reference
reference: article_reference

act_reference: abbreviated_act_id | (act_id from_now_on?)
from_now_on: "(a továbbiakban: " ABBREVIATION ")"
abbreviated_act_id: ABBREVIATION
act_id: ACT_YEAR "évi" ACT_NUMBER "törvény"
ACT_YEAR: NUMBER_WITH_DOT
ACT_NUMBER: ROMAN_WITH_DOT

article_reference: ARTICLE_ID "." "§"
ARTICLE_ID: NUMBER ("/" "A".."Z")?

quoted_text: "„" INSIDE_QUOTE quoted_text? INSIDE_QUOTE? "”"
INSIDE_QUOTE: /[^„”]/+

ABBREVIATION: UPPERCASE LOWERCASE+ "."

PARENTHESES: "(" | ")"
WORD: LETTER+

NUMBER: "0".."9"+
NUMBER_WITH_DOT: NUMBER "."
ROMAN_WITH_DOT: ("I" | "V" | "X" | "L" | "C" | "D" | "M")+ "."  //TODO: actual grammar for this.

WS: /[ \t\f\r\n]/+
UPPERCASE: /[A-ZÉÁŐÚÖÜÓÍŰ]/
LOWERCASE: /[a-zéáőúöüóíű]/
LETTER: /[a-zéáőúöüóíűA-ZÉÁŐÚÖÜÓÍŰ\-]/
END_OF_SENTENCE: "." | "?" | ":"
SECTION_SIGN: "§"

OTHER: /[^ „”§()?:\.\t\f\r\n1-9a-zéáőúöüóíűA-ZÉÁŐÚÖÜÓÍŰ\/\-]/

%ignore WS
%ignore OTHER

